buildscript {
    repositories {
        jcenter()
        //maven {
        //    name = "covers1624"
        //    url = "http://maven.covers1624.net/"
       // }
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'
apply plugin: 'maven-publish'

repositories {
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
}

// Define properties file
ext.configFile = file "build.properties"

configFile.withReader {
    // Load config.  It shall from now be referenced as simply config or project.config
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = "${config.minecraft_version}-${config.mod_version}"
group = "com.brandon3055.brandonscore" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "BrandonsCore"

sourceCompatibility = "1.7"
targetCompatibility = "1.7"

minecraft {
    version = config.minecraft_version + "-" + config.forge_version
    runDir = "run"
    mappings = "${config.mappings}"
    replace '${mod_version}', project.config.mod_version
    replaceIn "BrandonsCore.java"
    makeObfSourceJar = false
}

String buildNo = "22"
if (System.getenv("BUILD_NUMBER") != null) {
    buildNo = System.getenv("BUILD_NUMBER")
}
version = version + ".${buildNo}"

dependencies {
    compile "codechicken:CodeChickenLib:${config.minecraft_version}-${config.ccl_version}:deobf"
}


processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "ccl_version", config.ccl_version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include '*.info'

        // replace version and mcversion
        expand 'version': project.version, 'mc_version': project.minecraft.version, 'ccl_version': config.ccl_version
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

def commonManifest = {
    attributes 'FMLCorePluginContainsFMLMod': 'true'
//    attributes 'FMLCorePlugin': 'codechicken.core.launch.DepLoader'
    attributes 'FMLCorePlugin': 'com.brandon3055.brandonscore.asm.LoadingPlugin'
    attributes 'FMLAT': 'BrandonsCore_at.cfg'
}

jar {
    classifier = 'universal'
    manifest commonManifest
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
    manifest commonManifest
}

// Tell the artifact system about our extra jars
artifacts {
    archives srcJar, deobfJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("local_maven")) {
                repository(url: "file://${local_maven}")
                pom {
                    groupId = project.group
                    version = project.version
                    artifactId = project.archivesBaseName
                    project {
                        name project.archivesBaseName
                        packaging 'jar'
                        description 'BrandonsCore'
                        url 'https://github.com/brandon3055/BrandonsCore'
                        scm {
                            url 'https://github.com/brandon3055/BrandonsCore'
                            connection 'scm:git:git@github.com:brandon3055/BrandonsCore.git'
                            developerConnection 'scm:git:git@github.com:brandon3055/BrandonsCore.git'
                        }
                        issueManagement {
                            system 'github'
                            url 'https://github.com/brandon3055/BrandonsCore/issues'
                        }
                        licenses {
                            license {
                                name 'License'
                                url 'https://raw.githubusercontent.com/brandon3055/BrandonsCore/master/LICENSE.txt'
                                distribution 'repo'
                            }
                        }
                        developers {
                            developer {
                                id 'brandon3055'
                                name 'brandon3055'
                                roles { role 'developer' }
                            }
                        }
                    }
                }
            }
        }
    }
}
